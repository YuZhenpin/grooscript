buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.codehaus.groovy', name: 'groovy', version: '2.3.0-beta-1'
        classpath files("$buildDir/classes/main")
    }
}

import org.grooscript.GrooScript
import org.grooscript.gradle.TestMochaTask

def htmlBuilderSource = './src/main/groovy/org/grooscript/builder/HtmlBuilder.groovy'

task generateBuilderJs() << {
    GrooScript.setConversionProperty('initialText','//This script needs grooscript.js to run')
    GrooScript.convert(htmlBuilderSource, './src/main/resources/META-INF/resources')
}

task renameBuilderJs(dependsOn: generateBuilderJs, type: Copy) {
    from 'src/main/resources/META-INF/resources/HtmlBuilder.js'
    into 'src/main/resources/META-INF/resources'
    rename { String fileName ->
        fileName.replace('HtmlBuilder', 'grooscript-builder')
    }
}

task deleteBuilderJs(dependsOn: renameBuilderJs, type: Delete) {
    delete 'src/main/resources/META-INF/resources/HtmlBuilder.js'
}


task testJsBuilder(dependsOn: deleteBuilderJs, type: TestMochaTask) {
    file = 'src/test/js/testBuilder.js'
}

task generateGrooscriptBuilderJs(dependsOn: testJsBuilder) << {
    println 'Generated grooscript-builder.js'
}

generateGrooscriptBuilderJs.description = 'Generate grooscript-builder.js file'